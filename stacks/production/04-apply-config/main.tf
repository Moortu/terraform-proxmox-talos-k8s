# Providers and versions are generated by Terramate
# See config/generate_providers.tm.hcl

locals {
  talos_k8s_cluster_endpoint = "https://api.${var.talos_k8s_cluster_vip_domain}:${var.talos_k8s_cluster_endpoint_port}"
  network_ip_prefix          = tonumber(element(split("/", var.talos_network_cidr), 1))
}

# ============================================================================
# Reference Previous Stack Outputs
# ============================================================================

data "terraform_remote_state" "vms" {
  backend = "local"
  config = {
    path = "../02-vms/terraform.tfstate"
  }
}

data "terraform_remote_state" "talos_config" {
  backend = "local"
  config = {
    path = "../03-talos-config/terraform.tfstate"
  }
}

# ============================================================================
# Apply Talos Configurations to Control Plane Nodes
# ============================================================================

module "apply_config_cp" {
  source     = "../../../modules/talos/apply_config"

  providers = {
    talos = talos
  }

  machine_configuration = data.terraform_remote_state.talos_config.outputs.talos_config_cp
  client_configuration  = data.terraform_remote_state.talos_config.outputs.client_configuration
  nodes_network         = data.terraform_remote_state.vms.outputs.talos_control_plane_vms_network
  
  cluster_domain      = var.talos_k8s_cluster_domain
  cluster_endpoint    = local.talos_k8s_cluster_endpoint
  network_gateway     = var.talos_network_gateway
  network_ip_prefix   = local.network_ip_prefix
  cluster_vip         = var.talos_k8s_cluster_vip
}

# ============================================================================
# Apply Talos Configurations to Worker Nodes
# ============================================================================

module "apply_config_worker" {
  depends_on = [module.apply_config_cp]
  source     = "../../../modules/talos/apply_config"

  providers = {
    talos = talos
  }

  machine_configuration = data.terraform_remote_state.talos_config.outputs.talos_config_worker
  client_configuration  = data.terraform_remote_state.talos_config.outputs.client_configuration
  nodes_network         = data.terraform_remote_state.vms.outputs.talos_worker_network
  
  cluster_domain      = var.talos_k8s_cluster_domain
  cluster_endpoint    = local.talos_k8s_cluster_endpoint
  network_gateway     = var.talos_network_gateway
  network_ip_prefix   = local.network_ip_prefix
  cluster_vip         = var.talos_k8s_cluster_vip
}
